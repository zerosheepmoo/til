# 바쿠스-나우르 표기법

> [영문 위키](https://en.wikipedia.org/wiki/Backus%E2%80%93Naur_form)

```bnf
<symbol> ::= __expression__
```

- `<symbol>`은 nonterminal
- `__expression__`은 심볼의 시퀀스
- `|` 는 또는
- `::=` 는 좌항의 표현을 우항으로 대체 가능함을 나타냄
- 좌항에는 nonterminal 만 올 수 있음.

## 예시

- 주민등록번호

```bnf
<주민등록번호> ::= <생년월일> <주민등록번호-뒷자리> | <생년월일> "-" <주민등록번호-뒷자리>
<생년월일> ::= <연> <월> <일>
<연> ::= <숫자> <숫자>
<월> ::= "0" <숫자> | "1" <숫자-영부터이>
<일> ::= <숫자-영부터-삼><숫자>
<숫자> ::= <숫자-영부터삼> | "4" | "5" | "6" | "7" | "8" | "9"
<숫자-영부터삼> ::= <숫자-영부터이> | "3"
<숫자-영부터이> ::= <숫자-일부터이> | "0" 
<숫자-일부터이> ::= "1" | "2"
<성별> ::= <숫자-일부터이> | "3" | "4"
<동사무소번호> ::= <숫자> <숫자> <숫자> <숫자>
<태어난-순서> ::= <숫자>
<체크섬> ::= <숫자>
<임의번호> ::= <숫자> <숫자> <숫자> <숫자> <숫자> <숫자>
<주민등록번호-뒷자리> ::= <성별> <동사무소번호> <태어난-순서> <체크섬> | <임의번호>
```

- bnf를 bnf로

<!-- markdownlint-disable -->
```bnf
<syntax>         ::= <rule> | <rule> <syntax>
 <rule>           ::= <opt-whitespace> "<" <rule-name> ">" <opt-whitespace> "::=" <opt-whitespace> <expression> <line-end>
 <opt-whitespace> ::= " " <opt-whitespace> | ""
 <expression>     ::= <list> | <list> <opt-whitespace> "|" <opt-whitespace> <expression>
 <line-end>       ::= <opt-whitespace> <EOL> | <line-end> <line-end>
 <list>           ::= <term> | <term> <opt-whitespace> <list>
 <term>           ::= <literal> | "<" <rule-name> ">"
 <literal>        ::= '"' <text1> '"' | "'" <text2> "'"
 <text1>          ::= "" | <character1> <text1>
 <text2>          ::= '' | <character2> <text2>
 <character>      ::= <letter> | <digit> | <symbol>
 <letter>         ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
 <digit>          ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
 <symbol>         ::=  "|" | " " | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":" | ";" | ">" | "=" | "<" | "?" | "@" | "[" | "\" | "]" | "^" | "_" | "`" | "{" | "}" | "~"
 <character1>     ::= <character> | "'"
 <character2>     ::= <character> | '"'
 <rule-name>      ::= <letter> | <rule-name> <rule-char>
 <rule-char>      ::= <letter> | <digit> | "-"
```
<!-- markdownlint-enable -->